plugins {
	id "org.asciidoctor.jvm.convert"
}

configurations {
	javadoc
}

dependencies {
    javadoc project(path: ':subprojects:de.protos.etrice.gradle', configuration: 'javadocElements')
}

asciidoctor {
    outputDir = layout.buildDirectory.dir("site")
    attributes "version-plugin": project.version,
            "version-etrice": providers.gradleProperty("etriceVersion").orElse("5.4.0"),
            "version-gradle" : gradle.gradleVersion
    jvm {
        jvmArgs(
                "--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED",
                "--add-opens", "java.base/java.io=ALL-UNNAMED"
        )
    }
}

tasks.register('unzipJavadoc') {
    dependsOn(configurations.javadoc)
    doLast {
        project.sync {
            from(zipTree(configurations.javadoc.singleFile))
            into layout.buildDirectory.dir("site/javadoc")
        }
    }
}

tasks.register('buildSite') {
    dependsOn(asciidoctor, unzipJavadoc)
}