plugins {
	id "com.gradle.plugin-publish" apply false
}

subprojects {
	apply plugin: "java-library"
	apply plugin: "maven-publish"
	apply plugin: "java-gradle-plugin"
	apply plugin: "com.gradle.plugin-publish"

	java {
		withJavadocJar()
		withSourcesJar()
	}

	tasks.withType(JavaCompile) {
		if(JavaVersion.current() > JavaVersion.VERSION_1_8) {
			// Compile for minimum Java version supported by Gradle
			// https://docs.gradle.org/current/userguide/compatibility.html
			options.release = 8
		}
	}
}

task javadoc(type: Javadoc, group: "Documentation") {
	description = "Generates a global javadoc of all the modules"
	source subprojects.collect { it.sourceSets.main.allJava }
	classpath = files(subprojects.collect { it.sourceSets.main.compileClasspath })
	title = "eTrice Gradle Plugin Javadoc"
	options.links "https://docs.oracle.com/javase/8/docs/api/",
		"https://docs.gradle.org/$gradle.gradleVersion/javadoc/"
	destinationDir = file("$siteDir/javadoc")
}

buildSite.dependsOn javadoc