plugins {
	id "com.gradle.plugin-publish" apply false
}

subprojects {
	apply plugin: "java-library"
	apply plugin: "groovy"
	apply plugin: "maven-publish"
	apply plugin: "java-gradle-plugin"
	apply plugin: "com.gradle.plugin-publish"

	java {
		withJavadocJar()
		withSourcesJar()
	}

	dependencies {
		testImplementation libs.junit.jupiter
		testRuntimeOnly "org.junit.platform:junit-platform-launcher"
	}

	test {
		useJUnitPlatform()
	}

	compileJava {
		if(JavaVersion.current() > JavaVersion.VERSION_1_8) {
			// Compile for minimum Java version supported by Gradle
			// https://docs.gradle.org/current/userguide/compatibility.html
			options.release = 8
		}
		options.compilerArgs.addAll '-Xlint:all', '-Werror'  // treat compiler warnings as errors
	}

	javadoc {
		options.showFromPublic()  // show only public classes and members
		options.addBooleanOption 'Xdoclint:-missing', true  // silence warnings because of missing javadoc comments
		options.links "https://docs.oracle.com/en/java/javase/17/docs/api/", "https://docs.gradle.org/${gradle.gradleVersion}/javadoc/"
	}
}